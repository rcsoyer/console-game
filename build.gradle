plugins {
    id "idea"
    id "java"
    id "application"
    id "io.freefair.lombok" version "5.3.0"
}

java {
    sourceCompatibility = JavaVersion.VERSION_15
    targetCompatibility = JavaVersion.VERSION_15
}

ext {
    jUnitVersion = "5.7.1"
    mockitoVersion = "3.8.0"
    jvmArgEnablePreview = "--enable-preview"
}

tasks.withType(JavaCompile) {
    options.compilerArgs += jvmArgEnablePreview
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:${jUnitVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${jUnitVersion}"
    testRuntimeOnly "org.mockito:mockito-inline:${mockitoVersion}"
}

lombok {
    config["lombok.accessors.chain"] = "true"
    config["lombok.toString.callSuper"] = "call"
    config["lombok.equalsAndHashCode.callSuper"] = "skip"
    config["config.stopBubbling"] = "true"
}

test {
    useJUnitPlatform {
        jvmArgs += jvmArgEnablePreview
    }
}

run {
    main = "org.acme.game.GameApp"
    jvmArgs += jvmArgEnablePreview
    standardInput = System.in
}